#defaultView:Table
SELECT DISTINCT ?year ?name ?creators ?system ?wikidata {
  
{ SELECT DISTINCT
    ?item
    ?itemLabel
    ?date
    (GROUP_CONCAT(DISTINCT ?envLabel;separator=", ") AS ?system)
    (GROUP_CONCAT(DISTINCT ?creatorLabel;separator=", ") AS ?creators)
    (GROUP_CONCAT(DISTINCT ?short;separator="/") AS ?shortLabel)
  WHERE 
  {
    # hypertext systems
    { { ?item wdt:P31 wd:Q65966993 } UNION { ?item wdt:P279 wd:Q65966993 } }  
    
    # date of creation, publication etc.
    { { ?item wdt:P571 ?date } UNION { ?item wdt:P580 ?date } UNION { ?item wdt:P585 ?date } UNION { ?item wdt:P577 ?date } }
  
    # all kinds of creators
    OPTIONAL { 
      { ?item wdt:P50 ?creator } UNION  # author
      { ?item wdt:P170 ?creator } UNION # creator
      { ?item wdt:P61 ?creator } UNION  # discoverer or inventor
      { ?item wdt:P178 ?creator }       # developer
    }
    
    # operating system, programming language
    OPTIONAL {
      { ?item wdt:P306 ?env } UNION
      { ?item wdt:P277 ?env } 
    }
    
    # short name(s)
    OPTIONAL { ?item wdt:P1813 ?short } .
  
    SERVICE wikibase:label {
      bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en" .
      ?item rdfs:label ?itemLabel .
      ?creator rdfs:label ?creatorLabel .
      ?env rdfs:label ?envLabel .
    } .

  } GROUP BY ?item ?itemLabel ?date }

  FILTER NOT EXISTS {
    {  { ?item wdt:P571 ?date2 } UNION { ?item wdt:P580 ?date2 } UNION { ?item wdt:P585 ?date2 } UNION { ?item wdt:P577 ?date2 } }
    FILTER (?date > ?date2)
  } 
  
  BIND(IF(strlen(?shortLabel)>0,CONCAT(?itemLabel," (",?shortLabel,")"),?itemLabel) AS ?name)
  BIND(YEAR(?date) AS ?year)
  BIND(?item as ?wikidata)
          
} ORDER BY ?year
